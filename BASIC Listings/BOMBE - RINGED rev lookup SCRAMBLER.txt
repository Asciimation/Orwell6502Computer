

100 REM ENIGMA rotors and layout
110 REM   "ABCDEFGHIJKLMNOPQRSTUVWXYZ ABCDEFGHIJKLMNOPQRSTUVWXYZ" 
115 R1$ = "EKMFLGDQVZNTOWYHXUSPAIBRCJ UWYGADFPVZBECKMTHXSLRINQOJ" : REM I
120 R2$ = "AJDKSIRUXBLHWTMCQGZNPYFVOE AJPCZWRLFBDKOTYUQGENHXMIVS" : REM II
125 R3$ = "BDFHJLCPRTXVZNYEIWGAKMUSQO TAGBPCSDQEUFVNZHYIXJWLRKOM" : REM III
130 R4$ = "ESOVPZJAYQUIRHXLNFTGKDCMWB HZWVARTNLGUPXQCEJMBSKDYOIF" : REM IV  
135 R5$ = "VZBRGITYUPSDNHLXAWMJQOFECK QCYLXWENFTZOSMVJUDKGIARPHB" : REM V

150 REM ENIGMA refelctors
160 UB$ = "YRUHQSLDPXNGOKMIEBFZCWVJAT" : REM Reflector B
170 UC$ = "FVPJIAOYEDRZXWGCTKUQSBNMHL" : REM Reflector C

200 REM Bombe setup
210 D1$ = R3$ : REM Top drum
220 D2$ = R2$ : REM Middle drum
230 D3$ = R4$ : REM Bottom drum
240 RF$ = UB$ : REM Reflector

300 REM Some required variables
310 OF$ = "AAA" : REM Scrambler offset 
320 SV = 0 : REM Scrambler value
330 V$ = "" : REM Scrambler character
340 OD = 0 : REM Offset Current scrambler drum

1000 REM Main program
1010 PRINT : PRINT "ROTOR POSITION" : INPUT OF$
1020 PRINT : PRINT OF$
1030 PRINT : PRINT "RING" : INPUT RI
1040 PRINT : PRINT RI
1050 PRINT : PRINT "INPUT LETTER" : INPUT L$
1060 SV = ASC(L$) - 64
1065 GOSUB 2000
1070 PRINT : PRINT "OUTPUT LETTER" : PRINT V$
1080 GOTO 1010


2000 REM ---------- Scrambler subroutine ----------
2005 REM PRINT "INPUT: "; : PRINT CHR$(SV+64); 
2010 CD$ = D3$
2020 OD = ASC(MID$(OF$, 3, 1)) - 65
2030 GOSUB 2400 : REM Calculate scrambler offset
2040 GOSUB 2500 : REM Forward through rotor

2050 CD$ = D2$
2060 OD = ASC(MID$(OF$, 2, 1)) - 65
2070 GOSUB 2400 : REM Calculate scrambler offset
2080 GOSUB 2500 : REM Forward through rotor

2090 CD$ = D1$
2100 OD = ASC(MID$(OF$, 1, 1)) - 65
2110 GOSUB 2400 : REM Calculate scrambler offset
2120 GOSUB 2500 : REM Forward through rotor

2130 V$ = MID$(RF$, SV, 1)
2140 SV = ASC(V$) - 64 

2160 CD$ = D1$
2170 OD = ASC(MID$(OF$, 1, 1)) - 65
2180 GOSUB 2400 : REM Calculate scrambler offset
2190 GOSUB 2600 : REM Back through rotor

2200 CD$ = D2$
2210 OD = ASC(MID$(OF$, 2, 1)) - 65
2220 GOSUB 2400 : REM Calculate scrambler offset
2230 GOSUB 2600 : REM Back through rotor

2240 CD$ = D3$
2250 OD = ASC(MID$(OF$, 3, 1)) - 65
2260 GOSUB 2400 : REM Calculate scrambler offset
2270 GOSUB 2600 : REM Back through rotor

2275 REM PRINT " OUTPUT: "; : PRINT V$ 

2300 RETURN

2350 REM ---------- Wrap scrambler offset subroutine ----------
2360 IF SV < 1 THEN SV = SV + 26 : GOTO 2360
2370 IF SV > 26 THEN SV = SV - 26 : GOTO 2370
2380 RETURN

2400 REM ---------- Scrambler offset subroutine ----------
2410 SV = SV + OD - RI
2420 GOSUB 2350
2440 RETURN

2500 REM ---------- Forward through scrambler subroutine ----------
2510 V$ = MID$(CD$, SV, 1)  
2520 SV = (ASC(V$) - 64) - OD + RI
2530 GOSUB 2350 
2540 V$ = CHR$(SV+64)
2550 RETURN

2600 REM ---------- Back  through scrambler subroutine ----------
2610 V$ = MID$(CD$, SV + 27, 1)  
2620 SV = (ASC(V$) - 64) - OD + RI
2630 GOSUB 2350 
2640 V$ = CHR$(SV+64)
2650 RETURN




