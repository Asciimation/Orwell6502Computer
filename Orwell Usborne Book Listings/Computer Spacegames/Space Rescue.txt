10 CLS
20 PRINT "SPACE RESCUE"
30 PRINT
40 PRINT "DO YOU WANT INSTRUCTIONS? "
50 INPUT I$
60 IF LEFT$(I$,1)="Y" THEN GOSUB 1000
70 LET D=INT(RND(1)*800+101)
80 LET E=INT(RND(1)*400+401)
90 LET T=INT(D/SQR(E/5)+.5)
100 PRINT "THE PLANET IS ";D;" UNITS AWAY"
110 PRINT "YOU HAVE ";E;" UNITS OF ENERGY"
120 PRINT "AND A TIME LIMIT OF ";T;" DAYS"
130 PRINT
140 PRINT"ENERGY DISTRIBUTION:"
150 PRINT "TO ENGINES?"
160 INPUT P
170 PRINT "TO LIFE SUPPORT?"
180 INPUT L
190 PRINT "TO SHIELDS?"
200 INPUT S
210 IF P+L+S>E THEN GOTO 140
220 LET X=E-P-L-S
230 LET V=INT(SQR(P))
240 LET T1=INT(D/V)
250 CLS
260 PRINT "YOUR VELOCITY IS ";V
270 PRINT "YOU HAVE AN ETA OF ";T1;" DAYS"
280 PRINT
290 FOR I=1 TO INT(RND(1)*5+6)
300 IF RND(1)>.5 THEN GOTO 430
310 GOTO 320+INT(RND(1)*4)*30
320 PRINT "ASTEROID STORM - SHIELDS DAMAGED"
330 LET S=S-20-INT(RND(1)*40+1)
340 GOTO 430
350 PRINT "COMPUTER BREAKDOWN - DELAY IN REPAIRING"
360 LET D=D+INT(RND(1)*20+1)
370 GOTO 430
380 PRINT "ENGINE TROUBLE - MUST SLOW DOWN"
390 LET V=V-.5
400 GOTO 430
410 PRINT "X-RAY DAMAGE - LIFE SUPPORT DAMAGED"
420 LET L=L-20-INT(RND(1)*40+1)
430 FOR J=1 TO 750
440 NEXT J
450 NEXT I
460 LET T1=INT(D/V)
470 CLS
480 PRINT "ARRIVED IN ";T1;" DAYS"
490 IF S<0 THEN PRINT "SHIELDS DESTROYED"
495 IF S<0 THEN PRINT "YOU WERE BLOWN UP"
500 IF L<=0 THEN PRINT "LIFE SUPPORT INACTIVE"
505 IF L<=0 THEN PRINT "YOU'RE DEAD"
510 IF V<=0 THEN PRINT "ENGINESD ARE NON-FUNCTIONAL"
520 IF T1>T THEN PRINT "YOU TOOK TOO LONG ABOUT IT"
530 IF S<0 OR L<=0 OR V<=0 OR T1>T THEN STOP
540 LET G=INT(RND(1)*10+1)
550 LET G$="HIGH"
560 IF G<12 THEN LET G$="MEDIUM"
570 IF G<8 THEN LET G$="LOW"
580 LET A=INT(RND(1)*10+5)
590 LET A$="HIGH"
600 IF A<12 THEN LET A$="MEDIUM"
610 IF A<8 THEN LET A$="LOW"
620 PRINT
630 PRINT "YOU ARE NOW ORBITING THE PLANET"
640 PRINT "SURPLUS ENERGY=";X
650 PRINT "GRAVITY IS ";G$
660 PRINT "ATMOSPHERE IS ";A$
670 PRINT
680 PRINT "HOW MUCH ENERGY TO BOOSTERS?"
690 INPUT B
700 PRINT "HOW MUCH ENERGY TO HEAT SHIELDS?"
710 INPUT S
720 IF B+S>X THEN GOTO 680
730 CLS
740 IF B>=G*10 THEN GOTO 770
750 PRINT "YOU MADE A NEW CRATER"
760 GOTO 840
770 IF S>=A*10 THEN GOTO 800
780 PRINT "YOU MADE A WONDERFUL SHOOTING STAR"
790 GOTO 840
800 PRINT "YOU LANDED SUCCESSFULLY - WELL DONE"
810 IF X-S-B>25 THEN GOTO 840
820 PRINT "PITY YOU DON'T HAVE ENOUGH"
830 PRINT "ENERGY TO OPEN THE DOOR"
840 STOP
1000 PRINT 
1010 PRINT "YOU ARE ABOUT TO EMBARK ON A"
1020 PRINT "MISSION TO A DISTANT PLANET"
1030 PRINT "IN URGENT NEED OF MEDICAL"
1040 PRINT "SUPPLIES. YOU MUST FIRST READY"
1050 PRINT "YOUR SHIP FOR THE TRIP BY"
1060 PRINT "ALLOCATING SOME OF THE SHIP'S"
1070 PRINT "ENERY TO THE ENGINES, SHIELDS"
1080 PRINT "AND LIFE-SUPPORT. YOU ARE"
1090 PRINT "THEN PUT TO SLEEP FOR THE MAIN"
1100 PRINT "PART OF THE TRIP, AFTER WHICH"
1110 PRINT "YOU WILL GET A REPORT OF THE"
1120 PRINT "EVENTS ON THE WAY. YOU MUST"
1130 PRINT "THEN LAND ON THE PLANET....."
1140 PRINT "PRESS ANY KEY"
1150 GET I$ : IF I$="" THEN GOTO 1150
1160 CLS
1170 RETURN

