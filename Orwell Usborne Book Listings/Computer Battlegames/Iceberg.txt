10 PRINT "ICEBERG"
20 DIM B(8,8)
30 LET N=INT(RND(1)*8+1)
40 FOR I=1 TO N
50 LET B(INT(RND(1)*8+1),INT(RND(1)*8+1))=42
60 NEXT I
70 LET SX=INT(RND(1)*8+1)
80 LET SY=INT(RND(1)*8+1)
90 IF B(SX,SY)<>0 THEN GOTO 70
100 LET B(SX,SY)=90
110 LET YX=INT(RND(1)*8+1)
120 LET YY=INT(RND(1)*8+1)
130 IF B(YX,YY)<>0 THEN GOTO 110
140 LET B(YX,YY)=89
150 CLS
160 FOR Y=1 TO 8
170 FOR X=1 TO 8
180 IF B(X,Y)=0 THEN GOTO 210
190 PRINT CHR$(B(X,Y));
200 GOTO 220
210 PRINT ".";
220 PRINT " ";
230 NEXT X
240 PRINT
250 NEXT Y
260 LET B(YX,YY)=0
270 PRINT "DIRECTION (N,S,E,W) ";
280 INPUT D$
290 LET YY=YY-(D$="S" AND YY<>8)
300 LET YY=YY+(D$="N" AND YY<>1)
310 LET YX=YX-(D$="E" AND YX<>8)
320 LET YX=YX+(D$="W" AND YX<>1)
330 IF B(YX,YY)=90 THEN GOTO 500
340 IF B(YX,YY)=42 THEN GOTO 600
350 LET B(YX,YY)=89
360 LET B(SX,SY)=0
370 LET SX=SX+SGN(YX-SX)
380 LET SY=SY+SGN(YY-SY)
390 IF B(SX,SY)=89 THEN GOTO 500
400 IF B(SX,SY)=42 THEN GOTO 700
410 LET B(SX,SY)=90
420 GOTO 150
500 PRINT "YOU'VE BEEN CAUGHT"
510 GOTO 800
600 PRINT "YOU'VE HIT AN ICEBERG"
610 GOTO 800
700 PRINT "YOU'RE SAFE - HE'S HIT ONE"
800 STOP
