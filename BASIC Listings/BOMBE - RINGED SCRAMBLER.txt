
100 REM ENIGMA rotors and layout
110 REM  "ABCDEFGHIJKLMNOPQRSTUVWXYZ" 
115 R1$ = "EKMFLGDQVZNTOWYHXUSPAIBRCJ" : REM Rotor I
120 R2$ = "AJDKSIRUXBLHWTMCQGZNPYFVOE" : REM Rotor II
125 R3$ = "BDFHJLCPRTXVZNYEIWGAKMUSQO" : REM Rotor III
130 R4$ = "ESOVPZJAYQUIRHXLNFTGKDCMWB" : REM Rotor IV
135 R5$ = "VZBRGITYUPSDNHLXAWMJQOFECK" : REM Rotor V

150 REM ENIGMA refelctors
160 UB$ = "YRUHQSLDPXNGOKMIEBFZCWVJAT" : REM Reflector B
170 UC$ = "FVPJIAOYEDRZXWGCTKUQSBNMHL" : REM Reflector C

200 REM Bombe setup
210 D1$ = R3$ : REM Top drum
220 D2$ = R2$ : REM Middle drum
230 D3$ = R4$ : REM Bottom drum
240 RF$ = UB$ : REM Reflector

250 DIM DO(11) : REM Drum bank relative offsets

300 REM Some required variables
310 OF$ = "AAA" : REM Scrambler offset 
320 SV = 0 : REM Scrambler value
330 V$ = "" : REM Scrambler character
340 OD = 0 : REM Offset Current scrambler drum

1000 REM Main program
1010 PRINT : PRINT "ROTOR POSITION" : INPUT OF$
1020 PRINT : PRINT OF$
1030 PRINT : PRINT "RING" : INPUT RI
1040 PRINT : PRINT RI
1050 PRINT : PRINT "INPUT LETTER" : INPUT L$
1060 SV = ASC(L$) - 64


2000 REM Scrambler
2010 OD = ASC(MID$(OF$, 3, 1)) - 65
2020 SV = SV + OD - RI
2030 IF SV > 26 THEN SV = SV - 26
2035 IF SV < 1 THEN SV = SV + 26 
2040 V$ = MID$(D3$, SV, 1)  
2050 SV = (ASC(V$) - 64) - OD + RI
2060 IF SV < 1 THEN SV = SV + 26
2065 IF SV > 26 THEN SV = SV - 26
2070 V$ = CHR$(SV+64):PRINT V$;

2100 OD = ASC(MID$(OF$, 2, 1)) - 65
2110 SV = SV + OD - RI
2120 IF SV > 26 THEN SV = SV - 26
2125 IF SV < 1 THEN SV = SV + 26 
2130 V$ = MID$(D2$, SV, 1)  
2140 SV = (ASC(V$) - 64) - OD + RI
2150 IF SV < 1 THEN SV = SV + 26 
2155 IF SV > 26 THEN SV = SV - 26
2160 V$ = CHR$(SV+64):PRINT V$;

2200 OD = ASC(MID$(OF$, 1, 1)) - 65
2210 SV = SV + OD - RI
2220 IF SV > 26 THEN SV = SV - 26
2225 IF SV < 1 THEN SV = SV + 26 
2230 V$ = MID$(D1$, SV, 1)  
2240 SV = (ASC(V$) - 64) - OD + RI
2250 IF SV < 1 THEN SV = SV + 26 
2255 IF SV > 26 THEN SV = SV - 26
2260 V$ = CHR$(SV+64):PRINT V$;

2300 V$ = MID$(RF$, SV, 1)
2310 SV = ASC(V$) - 64 
2320 V$ = CHR$(SV+64):PRINT V$;

2400 OD = ASC(MID$(OF$, 1, 1)) - 65
2410 SV = SV + OD - RI
2420 IF SV > 26 THEN SV = SV - 26
2425 IF SV < 1 THEN SV = SV + 26 
2430 V$ = CHR$(SV+64)
2440 FOR I = 1 TO 26 
2450 IF MID$(D1$, I, 1) = V$ THEN SV = I: GOTO 2470
2460 NEXT I
2470 SV = SV - OD + RI
2480 IF SV < 1 THEN SV = SV + 26 
2485 IF SV > 26 THEN SV = SV - 26
2490 V$ = CHR$(SV+64):PRINT V$;

2500 OD = ASC(MID$(OF$, 2, 1)) - 65
2510 SV = SV + OD - RI
2520 IF SV > 26 THEN SV = SV - 26
2525 IF SV < 1 THEN SV = SV + 26 
2530 V$ = CHR$(SV+64)
2540 FOR I = 1 TO 26 
2550 IF MID$(D2$, I, 1) = V$ THEN SV = I : GOTO 2570
2560 NEXT I
2570 SV = SV - OD + RI
2580 IF SV < 1 THEN SV = SV + 26 
2585 IF SV > 26 THEN SV = SV - 26
2590 V$ = CHR$(SV+64):PRINT V$;

2600 OD = ASC(MID$(OF$, 3, 1)) - 65
2610 SV = SV + OD - RI
2620 IF SV > 26 THEN SV = SV - 26
2625 IF SV < 1 THEN SV = SV + 26 
2630 V$ = CHR$(SV+64)
2640 FOR I = 1 TO 26 
2650 IF MID$(D3$, I, 1) = V$ THEN SV = I : GOTO 2670
2660 NEXT I
2670 SV = SV - OD + RI
2680 IF SV < 1 THEN SV = SV + 26 
2685 IF SV > 26 THEN SV = SV - 26
2690 V$ = CHR$(SV+64):PRINT " "; : PRINT V$;

2700 GOTO 1010

3000 REM Increment offset
3010 L$ = LEFT$(OF$, 1)
3020 L = ASC(L$)
3030 L = L + 1 
3040 IF L > 90 THEN L = 65 
3050 L$ = CHR$(L) 
3060 OF$ = L$ + RIGHT$(OF$, 2)
3070 IF L$ <> "A" THEN GOTO 3300

3100 L$ = MID$(OF$, 2, 1)
3110 L = ASC(L$)
3120 L = L + 1
3130 IF L > 90 THEN L = 65
3140 L$ = CHR$(L)
3150 OF$ = LEFT$(OF$, 1) + L$ + RIGHT$(OF$, 1)
3160 IF L$ <> "A" THEN GOTO 3300

3200 L$ = RIGHT$(OF$, 1)
3210 L = ASC(L$)
3220 L = L + 1
3230 IF L > 90 THEN L = 65
3240 L$ = CHR$(L)
3250 OF$ = LEFT$(OF$,2) + L$

3300 RETURN



