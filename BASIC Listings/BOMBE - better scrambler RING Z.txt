
100 REM ENIGMA rotors and layout
110 REM   "ABCDEFGHIJKLMNOPQRSTUVWXYZ ABCDEFGHIJKLMNOPQRSTUVWXYZ" 
120 R1$ = "KMFLGDQVZNTOWYHXUSPAIBRCJE TVXFZCEOZYADBJLSGWRKQHMPNI" : REM I
130 R2$ = "JDKSIRUXBLHWTMCQGZNPYFVOEA ZIOBYVQKEACJNSXTPFDMGWLHUR" : REM II
140 R3$ = "DFHJLCPRTXVZNYEIWGAKMUSQOB SZFAOBRCPDTEUMYGXHWIVKQJNL" : REM III
150 R4$ = "SOVPZJAYQUIRHXLNFTGKDCMWBE GYVUZQSMKFTOWPBDILARDCXNHE" : REM IV  
160 R5$ = "ZBRGITYUPSDNHLXAWMJQOFECKV PBXKWVDMESYNRLUITCJFHZQOGA" : REM V

100 REM ENIGMA rotors and layout
110 REM   ABCDEFGHIJKLMNOPQRSTUVWXYZ ABCDEFGHIJKLMNOPQRSTUVWXYZ" 
120 R1$ = "EKMFLGDQVZNTOWYHXUSPAIBRCJ  UWYGADFPVZBECKMTHXSLRINQOJ" : REM I
130 R2$ = "AJDKSIRUXBLHWTMCQGZNPYFVOE  AJPCZWRLFBDKOTYUQGENHXMIVS" : REM II
140 R3$ = "BDFHJLCPRTXVZNYEIWGAKMUSQO  TAGBPCSDQEUFVNZHYIXJWLRKOM" : REM III
150 R4$ = "ESOVPZJAYQUIRHXLNFTGKDCMWB  HZWVARTNLGUPXQCEJMBSKDYOIF" : REM IV  
160 R5$ = "VZBRGITYUPSDNHLXAWMJQOFECK  QCYLXWENFTZOSMVJUDKGIARPHB" : REM V

220 REM ENIGMA refelctors
230 UB$ = "YRUHQSLDPXNGOKMIEBFZCWVJAT" : REM Reflector B
240 UC$ = "FVPJIAOYEDRZXWGCTKUQSBNMHL" : REM Reflector C

250 REM Bombe setup
260 D1$ = R3$ : REM Top drum
270 D2$ = R2$ : REM Middle drum
280 D3$ = R4$ : REM Bottom drum
290 RF$ = UB$ : REM Reflector

300 DIM DO(11) : REM Drum bank relative offsets

310 REM Some required variables
320 OF$ = "AAA" : REM Scrambler offset 
330 SV = 0 : REM Scrambler value
340 V$ = "" : REM Scrambler character
350 OD = 0 : REM Offset Current scrambler drum
360 TD$ = "" : REM Current scrambler rotor

1000 REM Main program -------------------------------------
1010 PRINT : PRINT "ROTOR POSITION" : INPUT OF$
1020 PRINT : PRINT "INPUT LETTER" : INPUT L$
1030 SV = ASC(L$) - 64
1040 GOSUB 2000
REM 1050 GOSUB 2000
1060 GOTO 1010

2000 REM Scrambler ----------------------------------------
2010 TD$ = D3$
2020 OD = ASC(MID$(OF$, 3, 1)) - 65
2030 GOSUB 2400 : REM Calculate offset
2040 GOSUB 2500 : REM Forward through rotor

2050 TD$ = D2$
2060 OD = ASC(MID$(OF$, 2, 1)) - 65
2070 GOSUB 2400 : REM Calculate offset
2080 GOSUB 2500 : REM Forward through rotor

2090 TD$ = D1$
2100 OD = ASC(MID$(OF$, 1, 1)) - 65
2110 GOSUB 2400 : REM Calculate offset
2120 GOSUB 2500 : REM Forward through rotor

2130 V$ = MID$(RF$, SV, 1)
2140 SV = ASC(V$) - 64 
2150 V$ = CHR$(SV+64):PRINT V$;

2160 TD$ = D1$
2170 OD = ASC(MID$(OF$, 1, 1)) - 65
2180 GOSUB 2400 : REM Calculate offset
2190 GOSUB 2600 : REM Back through rotor

2200 TD$ = D2$
2210 OD = ASC(MID$(OF$, 2, 1)) - 65
2220 GOSUB 2400 : REM Calculate offset
2230 GOSUB 2600 : REM Back through rotor

2240 TD$ = D3$
2250 OD = ASC(MID$(OF$, 3, 1)) - 65
2260 GOSUB 2400 : REM Calculate offset
2270 GOSUB 2600 : REM Back through rotor

2300 RETURN

2400 REM Calculate offset ---------------------------------
2410 SV = SV + OD
2420 IF SV > 26 THEN SV = SV - 26
2430 RETURN

2500 REM Forward through scrambler ------------------------
2510 V$ = MID$(TD$, SV, 1)  
2520 SV = (ASC(V$) - 64) - OD
2530 IF SV < 1 THEN SV = SV + 26 
2540 V$ = CHR$(SV+64):PRINT V$;
2550 RETURN

2600 REM Back through scrambler ---------------------------
2610 V$ = MID$(TD$, SV + 27, 1)  
2620 SV = (ASC(V$) - 64) - OD
2630 IF SV < 1 THEN SV = SV + 26 
2640 V$ = CHR$(SV+64):PRINT V$;
2650 RETURN




