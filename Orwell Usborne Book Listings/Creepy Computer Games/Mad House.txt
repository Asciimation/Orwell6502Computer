10 DIM P(3) 
20 DIM S(3)
30 CLS
40 PRINT "MAD HOUSE"
50 LET L$=""
60 LET W=38 
70 FOR I=1 TO W
80 LET L$=L$+"*"
90 NEXT I
100 LET CT=240
110 LET S(1)=1
120 LET S(3)=-1
130 FOR I=1 TO 3
140 LET P(I)=INT(RND(1)*(W-4)+1)
150 NEXT I
160 CLS
170 FOR I=5 TO 15 STEP 5
180 POKE 783,I: POKE 784,0: MOVE: PRINT L$
190 POKE 783,I: POKE 784,P(I/5): MOVE: PRINT "> <"
200 NEXT I
210 POKE 783,0: POKE 784,0: PRINT CT
220 IF CT<0 THEN GOTO 450
230 IF P(1)=P(2) AND P(2)=P(3) THEN GOTO 390
240 LET Z$ = INKEY$(1)
250 IF Z$="" THEN GOTO 300
260 IF Z$="X" THEN LET S(1)=-1
270 IF Z$="C" THEN LET S(1)=1
280 IF Z$="N" THEN LET S(3)=-1
290 IF Z$="M" THEN LET S(3)=1
300 IF CT/25=INT(CT/25) THEN LET P(2)=P(2)+INT(RND(1)*20)-10
310 LET CT=CT-1
320 LET P(1)=P(1)+S(1)
330 LET P(3)=P(3)+S(3)
340 FOR I=1 TO 3
350 IF P(I)<1 THEN LET P(I)=1
360 IF P(I)>(W-4) THEN LET P(I)=W-4
370 NEXT I
380 GOTO 170
390 LET L=P(1)+1
400 FOR I=1 TO 15
410 POKE 783,I: POKE 784,L: MOVE: PRINT "M"
420 NEXT I
430 PRINT "YOU ARE FREE !!"
440 STOP
450 PRINT "TOO LATE!  THE FOOTSTEPS HAVE STOPPED."
460 PRINT "ARGHHH!!!"
470 STOP
