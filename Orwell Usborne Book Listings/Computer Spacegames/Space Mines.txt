10 LET L=INT(RND(1)*3+5)
20 LET P=INT(RND(1)*60+40)
30 LET M=INT(RND(1)*50+10)*P
40 LET FP=INT(RND(1)*40+80)
50 LET CE=INT(RND(1)*40+80)
60 LET C=0
70 LET S=1
80 LET Y=1
90 LET LP=INT(RND(1)*2000+2000)
100 LET CP=INT(RND(1)*12+7)
110 CLS
120 PRINT "YEAR";Y
130 PRINT
140 PRINT "THERE ARE ";P;" PEOPLE IN THE COLONY."
150 PRINT "YOU HAVE ";L;" MINES, AND $"M
160 PRINT "SATISFACTION FACTOR ";S
170 PRINT
180 PRINT "YOUR MINES PRODUCED ";CE;" TONS EACH"
190 LET C=C+CE*L
200 PRINT "ORE IN STORE=";C;" TONS"
210 PRINT "SELLING"
220 PRINT "ORE SELLING PRICE=";CP
230 PRINT "MINE SELLING PRICE=";LP;"/MINE"
240 PRINT "HOW MUCH ORE TO SELL?"
250 INPUT CS
260 IF CS<0 OR CS>C THEN GOTO 240
270 LET C=C-CS
280 LET M=M+CS*CP
290 PRINT "HOW MANY MINES TO SELL?"
300 INPUT LS
310 IF LS<0 OR LS>L THEN GOTO 290
320 LET L=L-LS
330 LET M=M+LS*LP
340 PRINT
350 PRINT "YOU HAVE $";M
360 PRINT
370 PRINT "BUYING"
380 PRINT "HOW MUCH TO SPEND ON FOOD ? (APPR. $100 EA.) "
390 INPUT FB
400 IF FB<0 OR FB>M THEN GOTO 380
410 LET M=M-FB
420 IF FB/P>120 THEN LET S=S+.1
430 IF FB/P<80 THEN LET S=S-.2
440 PRINT "HOW MANY MORE MINES TO BUY?"
450 INPUT LB
460 IF LB<0 OR LB*LP>M THEN GOTO 440
470 LET L=L+LB
480 LET M=M-LB*LP
490 IF S<.6 THEN GOTO 660
500 IF S>1.1 THEN LET CE=CE+INT(RND(1)*20+1)
510 IF S<.9 THEN LET CE=CE-INT(RND(1)*20+1)
520 IF P/L<10 THEN GOTO 680
530 IF S>1.1 THEN LET P=P+INT(RND(1)*10+1)
540 IF S<.9 THEN LET P=P-INT(RND(1)*10+1)
550 IF P<30 THEN GOTO 700
560 IF RND(1)>.01 THEN GOTO 590
570 PRINT "RADIOACTIVE LEAK....MANY DIE"
580 LET P=INT(P/2)
590 IF CE<150 THEN GOTO 620
600 PRINT "MARKET GLUT - PRICE DROPS"
610 LET CE=INT(CE/2)
620 LET Y=Y+1
630 IF Y<11 THEN GOTO 90
640 PRINT "YOU SURVIVED YOUR TERM OF OFFICE"
650 STOP
660 PRINT "THE PEOPLE REVOLTED"
670 STOP
680 PRINT "YOU'VE OVERWORKED EVERYONE"
690 STOP
700 PRINT "NOT ENOUGH PEOPLE LEFT"
710 STOP
